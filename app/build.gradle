buildscript {
  repositories {
    google()
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
  }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "com.squareup.okhttptestapp"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 3
    versionName "1.3"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  lintOptions {
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }
  packagingOptions {
    exclude 'META-INF/INDEX.LIST'
    exclude 'META-INF/io.netty.versions.properties'
  }
  signingConfigs {
    config {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }
  buildToolsVersion '27.0.3'
}

dependencies {
  debugImplementation 'com.airbnb.okreplay:okreplay:1.4.0'
  releaseImplementation 'com.airbnb.okreplay:noop:1.4.0'
  androidTestImplementation 'com.airbnb.okreplay:espresso:1.4.0'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
  implementation 'io.reactivex.rxjava2:rxjava:2.1.15'
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation 'com.squareup.okhttp3:okhttp:3.11.0'
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation 'com.android.support:design:27.1.1'
  implementation 'com.android.support.constraint:constraint-layout:1.1.2'
  implementation 'com.google.android.gms:play-services-base:15.0.1'
  implementation 'com.android.support:support-core-utils:27.1.1'
  implementation 'com.android.support:support-compat:27.1.1'
  implementation 'com.android.support:support-vector-drawable:27.1.1'
  implementation 'com.android.support:support-annotations:27.1.1'
  implementation 'com.android.support:percent:27.1.1'
  implementation 'com.android.support:multidex:1.0.3'
  implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.24.0-eap13'
  implementation "org.jetbrains.anko:anko-sdk25:$anko_version"
  implementation "org.jetbrains.anko:anko-sdk25-coroutines:$anko_version"
  implementation "org.jetbrains.anko:anko-appcompat-v7:$anko_version"
  implementation 'io.zipkin.reporter2:zipkin-sender-okhttp3:2.7.7'
  implementation 'io.zipkin.brave:brave-okhttp:4.13.5'
  implementation 'io.zipkin.brave:brave-instrumentation-okhttp3:5.1.5'
  implementation 'io.zipkin.brave:brave:5.1.5'
  // ...
  // Litho
  implementation "com.facebook.litho:litho-core:$litho_version"
  implementation "com.facebook.litho:litho-widget:$litho_version"
  implementation "com.facebook.litho:litho-fresco:$litho_version"
  implementation "com.facebook.litho:litho-sections-core:$litho_version"
  implementation "com.facebook.litho:litho-sections-widget:$litho_version"
  implementation "com.facebook.litho:litho-annotations:$litho_version"
  implementation "com.facebook.litho:litho-sections-annotations:$litho_version"
  implementation 'com.facebook.flipper:flipper:0.6.16'
  kapt "com.facebook.litho:litho-processor:$litho_version"
  kapt "com.facebook.litho:litho-sections-processor:$litho_version"
  // SoLoader
  implementation 'com.facebook.soloader:soloader:0.5.1'
  // For testing
  testImplementation "com.facebook.litho:litho-testing:$litho_version"
  implementation 'com.bugsnag:bugsnag-android:4.3.1'
  implementation('com.baulsupp:okurl:1.52.0', {
    exclude group: 'org.jetbrains.kotlin'
    exclude group: 'com.google.guava'
    exclude module: 'oksocial-output'
    exclude module: 'completable-futures'
    exclude module: 'jfreesvg'
    exclude module: 'svg-salamander'
    exclude module: 'activation'
    exclude module: 'slf4j-jcl'
    exclude module: 'commons-logging'
    exclude module: 'netty-all'
    exclude group: 'org.slf4j'
  })
  implementation('com.baulsupp:oksocial-output:4.15.0', {
    exclude module: 'jfreesvg'
    exclude module: 'svg-salamander'
    exclude group: 'org.slf4j'
  })
  implementation 'org.slf4j:slf4j-android:1.7.25'
  implementation 'com.google.guava:guava:23.6-android'
  testImplementation 'junit:junit:4.12'
  androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
}
